plugins {
    id 'org.springframework.boot' version '2.4.3' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

apply plugin: 'base'

allprojects {
    group 'io.rsocket.examples'
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://oss.jfrog.org/artifactory/libs-snapshot' }
        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.spring.io/snapshot' }
    }

    dependencies {
        [
         enforcedPlatform('io.rsocket:rsocket-bom:1.1.1-SNAPSHOT'),
         enforcedPlatform('org.springframework.boot:spring-boot-dependencies:2.4.3'),
         platform('org.junit:junit-bom:5.4.0'),
         platform('io.projectreactor:reactor-bom:2020.0.4'),
         platform('io.github.resilience4j:resilience4j-bom:1.7.0'),
        ].each {
            implementation it
            annotationProcessor it
            testAnnotationProcessor it
        }

        constraints {
            ['org.projectlombok:lombok:1.18.18'].each {
                compileOnly it
                annotationProcessor it
                testAnnotationProcessor it
            }

            ['com.github.javafaker:javafaker:1.0.2',
             'io.dropwizard.metrics:metrics-core:+',
             'org.beryx:text-io:3.4.1',
             'org.beryx:text-io-web:3.4.1',
             'org.fusesource.jansi:jansi:2.3.1',
             'org.codehaus.groovy:groovy-all:3.0.7',
             'com.google.guava:guava:27.1-jre',
            ].each {
                implementation it
            }

            ['org.mockito:mockito-core:2.22.0',
             'org.mockito:mockito-junit-jupiter:2.22.0',
             'io.github.benas:random-beans:3.7.0',
            ].each {
                testImplementation it
            }
        }

        //declare dependencies
        ['org.projectlombok:lombok',
         'org.springframework.boot:spring-boot-configuration-processor',
        ].each {
            compileOnly it
            testCompileOnly it
            annotationProcessor it
            testAnnotationProcessor it
        }

        implementation 'org.springframework.boot:spring-boot-starter-rsocket',
                'io.rsocket:rsocket-transport-netty',
                'com.google.guava:guava',
                'io.dropwizard.metrics:metrics-core',
                'org.beryx:text-io',
                'org.beryx:text-io-web',
                'org.fusesource.jansi:jansi',
                'io.projectreactor.addons:reactor-extra'


        testImplementation 'org.mockito:mockito-core',
                'org.mockito:mockito-junit-jupiter',
                'org.junit.jupiter:junit-jupiter-engine',
                'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
            events 'passed', 'skipped', 'failed'
        }
        reports {
            html.enabled = true
        }
    }
}
